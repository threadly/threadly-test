apply plugin: 'java'

repositories {
  mavenCentral()
}

dependencies {
  testImplementation (
    "junit:junit:4.12",
  )

  implementation (
    "org.threadly:threadly:$threadlyVersion"
  )
}

compileJava {
  options.compilerArgs << "-Xlint:all" << "-Xlint:-deprecation" << "-Werror"

  if (JavaVersion.current().isJava8()) {
    options.compilerArgs << "-XDenableSunApiLintControl" << "-Xlint:-sunapi"
  }
}

compileTestJava {
  options.compilerArgs << "-Xlint:all"
}

test {
  getReports().getJunitXml().setDestination(file("$buildDir/reports/tests/xml"))
  getReports().getHtml().setDestination(file("$buildDir/reports/tests/html"))
  setBinResultsDir(file("$buildDir/reports/tests/bin"))
}

jar {
  manifest {
    attributes (
      'Implementation-Title': 'Threadly Test Utilities', 
      'Implementation-Version': archiveVersion
    )
  }
}

javadoc {
  source = sourceSets.main.allJava
  excludes = ['**/ThreadlyInternalAccessor**', '**/ArgumentVerifier**']
  options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PUBLIC
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

build.dependsOn("copyLibs");

task copyLibs(type: Copy) {
  into "$buildDir/dependencies/"
  from configurations.runtimeClasspath
}

artifacts {
  //archives jar // already specified by default
  archives javadocJar
  archives sourcesJar
}
